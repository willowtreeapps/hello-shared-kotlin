apply plugin: 'kotlin'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib"
}

apply plugin: 'konan'

konan.targets = ['iphone', 'iphone_sim']

def frameworkName = 'KotlinHello'

konanArtifacts {
    framework(frameworkName)
}

task lipo(type: Exec, dependsOn: 'build') {
    def frameworks = files(
            "$buildDir/konan/bin/iphone/${frameworkName}.framework/$frameworkName",
            "$buildDir/konan/bin/iphone_sim/${frameworkName}.framework/$frameworkName"
    )
    def output = file("$buildDir/konan/bin/iphone_universal/${frameworkName}.framework/$frameworkName")
    inputs.files frameworks
    outputs.file output
    executable = 'lipo'
    args = frameworks.files
    args += ['-create', '-output', output]
}

task copyFramework(type: Copy, dependsOn: lipo) {
    from("$buildDir/konan/bin/iphone") {
        include '*/Headers/*'
        include '*/Modules/*'
        include 'Info.plist'
    }
    from "$buildDir/konan/bin/iphone_universal"
    into "${rootProject.rootDir}/ios"
}
